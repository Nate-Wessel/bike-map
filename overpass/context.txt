// define a global bounding box (s,w,n,e)
[bbox:43.5,-79.7,43.9,-79.0];

// select ways and relations of interest from within the bounding box
(

	way[waterway~'river*'];
	rel[waterway=riverbank];
  
	way[natural~'wood|beach|wetland|water'];
	rel[natural=water];
	node[natural=tree];

	way[leisure~'^park$|golf_course'];
	rel[leisure~'^park$|golf_course'];
  
	way[landuse~'cemetery|industrial|railway|forest'];

	way[amenity=university];
	rel[amenity=university];

	rel[route=train][operator~'GO Transit|Union Pearson Express'];

	way[aeroway~'runway|aerodrome|taxiway|terminal'];
	rel[aeroway~'aerodrome|terminal'];

	nwr[shop=bicycle];
	nwr[amenity=bicycle_rental];

	rel(324211); // City of Toronto

)->.prefilter;

// filter out data we don't want
(
	( // keep everything except CanVec sources
		nwr.prefilter[!source];
		nwr.prefilter[source!~'CanVec'];
	);
	-
	( // exclude two giant lakes from the above set
		rel[name='Lake Huron'];
		rel[name='Lake Erie'];
	);
)->.filtered;

(.filtered;>;); 

out body qt;
